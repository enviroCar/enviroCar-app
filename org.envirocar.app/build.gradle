apply plugin: 'com.android.application' //or apply plugin: 'java'
apply plugin: "com.github.hierynomus.license"

dependencies {
    // Android dependencies.
    implementation rootProject.ext.supportV4
    implementation rootProject.ext.supportV7
    implementation rootProject.ext.supportDesign
    implementation rootProject.ext.supportCardview
    implementation rootProject.ext.supportRecyclerview
    implementation rootProject.ext.supportConstraintLayout

    // Dependency injection, view injection, event bus ...
    implementation rootProject.ext.dagger
    implementation rootProject.ext.otto
    implementation rootProject.ext.butterKnife
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor rootProject.ext.daggerCompiler
    annotationProcessor rootProject.ext.butterknifeCompiler
    //    implementation 'org.glassfish:javax.annotation:10.0-b28'

    // Google dependencies
    implementation rootProject.ext.gson
    implementation rootProject.ext.playServicesLocation
    implementation rootProject.ext.findBugs

    // RxJava for Android
    implementation rootProject.ext.rxAndroid
    implementation rootProject.ext.rxJava
    implementation rootProject.ext.rxPreferences
    implementation rootProject.ext.rxBinding

    // other third party libraries
    implementation rootProject.ext.acra
    implementation rootProject.ext.helloCharts
    implementation rootProject.ext.fabProgressCircle
    implementation rootProject.ext.segmentedUIController
    implementation rootProject.ext.materialDialogs

    //implementation rootProject.ext.guava
//    implementation(materialDialogs) {
//        exclude module: 'appcompat-v7'
//        exclude module: 'recyclerview-v7'
//        exclude module: 'support-annotations'
//        transitive = true
//    }


    // MapBox SDK
    implementation(rootProject.ext.mapbox) {
        transitive = true
    }

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation('org.powermock:powermock-api-mockito:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testImplementation('org.powermock:powermock-module-junit4:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test:runner:1.1.0-alpha4') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test:rules:1.1.0-alpha4') {
        exclude module: 'support-annotations'
    }

    // modules
    api project(path: ':org.envirocar.core')
    api project(path: ':org.envirocar.remote')
    api project(path: ':org.envirocar.obd')
    api project(path: ':org.envirocar.storage')
    api project(path: ':org.envirocar.algorithm')
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        test.setRoot('tests/unit')
        androidTest.setRoot('tests/android')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
    }
}
